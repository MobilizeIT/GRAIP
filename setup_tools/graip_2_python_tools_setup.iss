; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "GRAIP-2 Python Tools"
#define MyAppVersion "1.1"
#define MyAppPublisher "Utah State University"
#define MyAppURL "http://www.neng.usu.edu/cee/faculty/dtarb/tarsoftware.html"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{37666F9C-A3A3-48EA-9B36-A232206FC314}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\GRAIP-2
DefaultGroupName={#MyAppName}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
; On all other architectures it will install in "32-bit mode".
ArchitecturesInstallIn64BitMode=x64
WizardSmallImageFile=graip.bmp
; Don't show the welcome wizard page  and ready to install page
DisableWelcomePage=yes
DisableReadyPage=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "SourceFiles\ArcGISCalibrationRegionTool.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGISRoadSurfaceErosion.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGisStreamSedimentInput.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGISStabilityIndex.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGISStabilityIndexWithRoadImpact.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGISCalibrationRegionTool.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\ArcGISMassWastingPotential.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion

Source: "SourceFiles\consolidateShp.dll"; DestDir: "{app}\Preprocessor"; Flags: restartreplace ignoreversion regserver 32bit
Source: "SourceFiles\consolidateShp.dll"; DestDir: "{app}\Preprocessor"; Flags: restartreplace ignoreversion regserver 64bit; Check: IsWin64

Source: "SourceFiles\demo.zip"; DestDir: "{app}\Documentation"; Flags: ignoreversion
Source: "SourceFiles\GRAIP Tools.tbx"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\GRAIP.mdb"; DestDir: "{app}\Preprocessor\graip db"; Flags: ignoreversion
Source: "SourceFiles\GRAIPPreprocessor.exe"; DestDir: "{app}\Preprocessor"; Flags: ignoreversion

Source: "SourceFiles\CalibrationRegionTool.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\RoadSurfaceErosion.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\StreamSedimentInput.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\StabilityIndex.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\CalibrationRegionTool.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\MassWastingPotential.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion
Source: "SourceFiles\utils.py"; DestDir: "{app}\PythonTools"; Flags: ignoreversion

; we no more installing sinmap. GRIAP-2 version 1.1 will depend on TauDEM 5.3 which should have sianmap installed
;Source: "E:\SoftwareProjects\GRAIPPythonTools\setup_tools\SourceFiles\SinmapSI\32_bit\SinmapSI.exe"; DestDir: "{app}\PythonTools"; Flags: ignoreversion; Check: not IsWin64
;Source: "E:\SoftwareProjects\GRAIPPythonTools\setup_tools\SourceFiles\SinmapSI\64_bit\SinmapSI.exe"; DestDir: "{app}\PythonTools"; Flags: ignoreversion; Check: IsWin64 

Source: "SourceFiles\comdlg32.ocx"; DestDir: {sys}; Flags: onlyifdoesntexist regserver 32bit
Source: "SourceFiles\mshflxgd.ocx"; DestDir: {sys}; Flags: onlyifdoesntexist regserver 32bit
Source: "SourceFiles\MSCOMCTL.OCX"; DestDir: {sys}; Flags: onlyifdoesntexist regserver 32bit
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\Preprocessor"; Filename: "{app}\Preprocessor\GRAIPPreprocessor.exe"

[Registry]
; set PATH  for PythonTools
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType:string; ValueName:"PATH"; ValueData:"{code:GetPathData|{olddata}}"; Flags: preservestringtype


[code]
// add a custome wizard page after the welcome page to show the list of programs that will be installed
procedure InitializeWizard();
var UserPage_1: TInputQueryWizardPage;
var UserPage_2: TInputQueryWizardPage;
var notes_string_1: string;
var notes_string_2: string;

begin
  notes_string_1 := 'NOTES:With the installation of GRAIP-2 Python tools, the following python scripts are installed:'#13 +
      '1. GRAIP Tools.tbx: This is an ArcGIS Toolbox that interfaces to Python Scripts'#13 +
      '2. ArcGISRoadSurfaceErosion.py: ArcGIS Toolbox interface to Road Surface Erosion function'#13 +
      '3. ArcGISStreamSedimentInput.py: ArcGIS Toolbox interface to Stream Sediment Input function'#13 +
      '4. ArcGISStabilityIndex.py: ArcGIS Toolbox Stability Index interface to Stability Index function'#13 +
      '5. ArcGISStabilityIndexWithRoadImpact.py: ArcGIS Toolbox Stability Index with Road Impact interface to Stability Index function'#13 +
      '6. ArcGISCalibrationRegionTool.py: ArcGIS Toolbox Calibration Region Tool interface to Calibration Region Tool function'#13 +
      '7. ArcGISMassWasting.py: ArcGIS Toolbox Mass Wasting Potential Analysis interface to Mas Wasting Potential function';
      
  notes_string_2 := '8. RoadSurfaceErosion.py: Command line callable Road Surface Erosion function'#13 +
      '9. StreamSedimentInput.py: Command line callable Stream Sediment Input function'#13 +
      '10. RoadSurfaceErosion.py: Command line callable Road Surface Erosion function'#13 +
      '11. StabilityIndex.py: Command line callable Stability Index function'#13 +
      '12. CalibrationRegionTool.py: Command line callable Calibration Region Tool function'#13 +
      '13. MassWastingPotential.py: Command line callable Mas Wasting Potential function'#13#13 +
      'The installer will also add this path entry:C:\Program Files\GRAIP-2\PythonTools'; 

  UserPage_1 := CreateInputQueryPage(wpWelcome,
      'The following programs will be installed', '',
      'GRAIPPreprocessor.exe (GRAIP 1.0.10), GRAIP-2 Python tools version 1.1'#13 +  notes_string_1);  

  UserPage_2 := CreateInputQueryPage(UserPage_1.ID,
  'Continued: The following programs will be installed', '', 
  'The following python scripts are installed:'#13 + notes_string_2); 
  
end;

function GetPathData(Param: String): String;
{ The 'Param' parameter has the value of the existing value for the system 'Path' variable }
var 
  index: integer;
  dataToAdd: string;
begin 
  { This is the data we want to add to the system 'Path' variable} 
  dataToAdd:= ExpandConstant('{app}') + '\PythonTools\';

  { check if the data we want to add is already in the 'Path' variable }
  index:=pos(dataToAdd, Param)
  if index > 0 then
     { no need to change data for the path - so return the existing data}
     Result:= Param
  else
    { append new data to existing path data and return the resultant data }
    Result:= Param + ';' + dataToAdd; 
end;