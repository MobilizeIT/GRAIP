; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "GRAIP-2 Prerequisites"
#define MyAppVersion "1.0"
#define MyAppPublisher "Utah State University"
#define MyAppURL "http://www.neng.usu.edu/cee/faculty/dtarb/graip/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{247BA42D-62F4-4672-BF90-4E1C28E48A3F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=graip_python_setup
Compression=lzma
SolidCompression=yes
; Tell Windows Explorer to reload the environment
ChangesEnvironment=yes

[Files]
; Install GDAL python library
Source: "C:\GDAL\gdalwin32-1.6\*"; DestDir: "{app}\GDAL\\gdalwin32-1.6"; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall 

; Install Proj python library
Source: "C:\GDAL_Proj\proj\*"; DestDir: "{app}\GDAL_Proj\proj"; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall 

; Install Click python comandline interface library
Source: "C:\Click\click-2.5\*"; DestDir: "{app}\Click\click-2.5"; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall 

; Install all files in the setup_files - these are executable files that inno setup will run. see [Run] section
Source: "E:\SoftwareProjects\GRAIPPythonTools\setup\setup_files\*"; DestDir: "{app}\setup_files"; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall 

[Run]
; install GDAL python bindings
Filename: "{app}\setup_files\GDAL-1.9.2.win32-py2.7.exe"; Flags: waituntilterminated
; install numpy
Filename: "{app}\setup_files\numpy-1.8.1-win32-superpack-python2.7.exe"; Flags: waituntilterminated
; install pyodbc
Filename: "{app}\setup_files\pyodbc-3.0.7.win32-py2.7.exe"; Flags: waituntilterminated

; >>>TODO: The set_proj_lib.py file have code to copy the proj.dll file from the Proj installtion bin directory to the GDAL bin directory
; also delete every thing from the Proj lib's nad directory
;Filename: "cmd.exe"; Parameters: "/{app}\setup_files\set_proj_lib.py"; WorkingDir: "{app}"; Flags: waituntilterminated
;Filename: "cmd.exe"; Parameters: "/{app}\setup_files\ez_setup.py, /install"; WorkingDir: "{app}"; Flags: shellexec, waituntilterminated
Filename: "{app}\setup_files\ez_setup.py"; Parameters: "install"; WorkingDir: "{app}"; Flags: shellexec waituntilterminated runascurrentuser
;Filename: "cmd.exe"; Parameters: "/{app}\setup_files\hello.py"; WorkingDir: "{app}"; Flags: waituntilterminated
; run the setup.py file from the Click installation to register it as a python package
;Filename: "cmd.exe"; Parameters: "python {app}\Click\click-2.5\setup.py /install"; WorkingDir: "{app}"; Flags: waituntilterminated runascurrentuser
Filename: "{app}\Click\click-2.5\setup.py"; Parameters: "install"; Flags: shellexec waituntilterminated runascurrentuser



[Registry]
; set PATH
;Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType:string; ValueName:"PATH"; ValueData:"{olddata};{app}\GDAL\gdalwin32-1.6\bin; {app}\GDAL_Proj\proj\bin"; Flags: preservestringtype
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType:string; ValueName:"PATH"; ValueData:"{code:GetPathData|{olddata}}"; Flags: preservestringtype
; set GDAL_DATA
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType:string; ValueName:"GDAL_DATA"; ValueData:"{app}\GDAL\gdalwin32-1.6\data"; Flags: preservestringtype
; set PROJ_LIB
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType:string; ValueName:"PROJ_LIB"; ValueData:"{app}\GDAL_Proj\proj\nad"; Flags: preservestringtype

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[UninstallDelete]
Type: filesandordirs; Name: "{app}"


[code]
function GetPathData(Param: String): String;
{ The 'Param' parameter has the value of the existing value for the system 'Path' variable }
var 
  index: integer;
  dataToAdd: string;
begin 
  { This is the data we want to add to the system 'Path' variable} 
  dataToAdd:= ExpandConstant('{app}') + '\GDAL\gdalwin32-1.6\bin;' +  ExpandConstant('{app}') + '\GDAL_Proj\proj\bin';

  { check if the data we want to add is already in the 'Path' variable }
  index:=pos(dataToAdd, Param)
  if index > 0 then
     { no need to change data for the path - so return the existing data}
     Result:= Param
  else
    { append new data to existing path data and return the resultant data }
    Result:= Param + ';' + dataToAdd; 
end;


